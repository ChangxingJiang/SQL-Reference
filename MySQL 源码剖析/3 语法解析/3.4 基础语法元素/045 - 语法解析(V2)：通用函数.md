目录文档：[MySQL 源码｜源码剖析文档目录](https://zhuanlan.zhihu.com/p/714761054)

源码位置（版本 = MySQL 8.0.37）：[sql/sql_yacc.yy](https://github.com/mysql/mysql-server/blob/trunk/sql/sql_yacc.yy)

前置文档：

- [MySQL 源码｜41 - 语法解析(V2)：标识符 IDENT](https://zhuanlan.zhihu.com/p/714782314)
- [MySQL 源码｜65 - 语法解析(V2)：字符串字面值](https://zhuanlan.zhihu.com/p/717934287)

---

在上一节中，我们梳理了 `function_call_nonkeyword` 语义组，这节我们继续梳理通用函数语义组 `function_call_generic`，其涉及语义组和语义组之间的关系如下图所示（图中绿色节点为字符串字面值涉及节点、蓝色节点为其他语义组、灰色节点为其他终结符）：

![语法解析-011-通用函数](C:\blog\graph\MySQL源码剖析\语法解析-011-通用函数.png)

#### 语义组：`function_call_generic`

`function_call_generic` 语义组用于解析除关键字函数、非关键字函数、以及需为避免语法冲突专门处理的函数以外的其他通用函数。

- 官方文档：[MySQL 参考手册 - Chapter 14 Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/functions.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类
- 备选规则和 Bison 语法如下：

| 备选规则                                | 规则含义                            |
| --------------------------------------- | ----------------------------------- |
| `IDENT_sys '(' opt_udf_expr_list ')'`   | 函数名为一个标识符的函数            |
| `ident '.' ident '(' opt_expr_list ')'` | 函数名为 `{标识符}.{标识符}` 的函数 |

```C++
function_call_generic:
          IDENT_sys '(' opt_udf_expr_list ')'
          {
            $$= NEW_PTN PTI_function_call_generic_ident_sys(@$, $1, $3);
          }
        | ident '.' ident '(' opt_expr_list ')'
          {
            $$= NEW_PTN PTI_function_call_generic_2d(@$, $1, $3, $5);
          }
        ;
```

> `IDENT_sys` 语义组解析没有引号的标识符名称（包含或不包含多字节字符），`ident` 语义组解析标识符或任意未保留关键字，详见 [MySQL 源码｜41 - 语法解析：标识符 IDENT](https://zhuanlan.zhihu.com/p/714782314)。

#### 语义组：`opt_udf_expr_list`

`opt_udf_expr_list` 语义组用于解析可选的 UDF 函数参数列表。

- 返回值类型：`PT_item_list` 类（`item_list2`）
- Bison 语法如下：

```C++
opt_udf_expr_list:
          %empty { $$= nullptr; }
        | udf_expr_list { $$= $1; }
        ;
```

#### 语义组：`udf_expr_list`

`udf_expr_list` 语义组用于解析任意数量、逗号分隔的 UDF 函数参数的列表。

- 返回值类型：`PT_item_list` 类（`item_list2`）
- Bison 语法如下：

```C++
udf_expr_list:
          udf_expr
          {
            $$= NEW_PTN PT_item_list(@$);
            if ($$ == nullptr || $$->push_back($1))
              MYSQL_YYABORT;
          }
        | udf_expr_list ',' udf_expr
          {
            if ($1 == nullptr || $1->push_back($3))
              MYSQL_YYABORT;
            $$= $1;
            // This will override earlier udf_expr_list, until
            // we get the whole location.
            $$->m_pos = @$;
          }
        ;
```

#### 语义组：`udf_expr`

`udf_expr` 语义组用于解析单个 UDF 函数参数，由一般表达式（`expr` 语义组）和可选的别名子句（`select_alias`）组成。

- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。
- Bison 语法如下：

```C++
udf_expr:
          expr select_alias
          {
            $$= NEW_PTN PTI_udf_expr(@$, $1, $2, @1.cpp);
          }
        ;
```

#### 语义组：`select_alias`

`select_alias` 语义组用于解析可选的 `AS` 关键字引导（可省略）别名子句。

- 官方文档：[MySQL 参考手册 - 15.2.13 SELECT Statement](https://dev.mysql.com/doc/refman/8.0/en/select.html)
- 标准语法：`[[AS] alias]`
- 返回值类型：`MYSQL_LEX_STRING` 结构体（`lexer.lex_str`），包含字符串指针和长度
- 备选规则和 Bison 语法如下：

| 备选规则                   | 规则含义                               |
| -------------------------- | -------------------------------------- |
| `%empty`                   | 不匹配                                 |
| `AS ident`                 | `AS` 关键字引导一个标识符              |
| `AS TEXT_STRING_validated` | `AS` 关键字引导一个带引号的字符串      |
| `ident`                    | 一个标识符（省略 `AS` 关键字）         |
| `TEXT_STRING_validated`    | 一个带引号的字符串（省略 `AS` 关键字） |

```C++
select_alias:
          %empty { $$=null_lex_str;}
        | AS ident { $$=$2; }
        | AS TEXT_STRING_validated { $$=$2; }
        | ident { $$=$1; }
        | TEXT_STRING_validated { $$=$1; }
        ;
```

> `ident` 语义组解析标识符或任意未保留关键字，详见 [MySQL 源码｜41 - 语法解析：标识符 IDENT](https://zhuanlan.zhihu.com/p/714782314)；
>
> `TEXT_STRING_validated` 用于解析作为字段别名的单引号 / 双引号字符串，详见 [MySQL 源码｜65 - 语法解析(V2)：字符串字面值](https://zhuanlan.zhihu.com/p/717934287)。
