目录文档：[MySQL 源码｜源码剖析文档目录](https://zhuanlan.zhihu.com/p/714761054)

源码位置（版本 = MySQL 8.0.37）：[sql/sql_yacc.yy](https://github.com/mysql/mysql-server/blob/trunk/sql/sql_yacc.yy)

前置文档：

- [MySQL 源码｜38 - 语法解析(V2)：窗口函数](https://zhuanlan.zhihu.com/p/714780506)

---

在上一节（[MySQL 源码｜38 - 语法解析(V2)：窗口函数](https://zhuanlan.zhihu.com/p/714780506)）中，我们梳理了 `window_func_call` 语义组，本节我们继续梳理用于解析聚集函数的 `sum_expr` 语义组，其涉及语义组和语义组之间的关系如下图所示（图中绿色节点为字符串字面值涉及节点、蓝色节点为其他语义组、灰色节点为其他终结符）：

![语法解析-008-聚集函数](C:\blog\graph\MySQL源码剖析\语法解析-008-聚集函数.png)

#### 语义组：`sum_expr`

`sum_expr` 语义组用于解析既是窗口函数，又是聚集函数的函数。

- 官方文档：[MySQL 8.0 参考手册：14.19.1 Aggregate Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/aggregate-functions.html#function_json-arrayagg)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。

语义组中的备选规则如下：

##### `AVG` 函数

标准语法：`AVG([DISTINCT] expr) [over_clause]`

用于计算 `in_sum_expr` 的平均值。有 `AVG(in_sum_expr)` 和 `AVG(DISTINCT in_sum_expr)` 两种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
          AVG_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_avg(@$, $3, false, $5);
          }
        | AVG_SYM '(' DISTINCT in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_avg(@$, $4, true, $6);
          }
```

##### `BIT_AND` 函数

标准语法：`BIT_AND(expr) [over_clause]`

用于计算 `in_sum_expr` 的按位与。只有 `BIT_AND(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | BIT_AND_SYM  '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_and(@$, $3, $5);
          }
```

##### `BIT_OR` 函数

标准语法：`BIT_OR(expr) [over_clause]`

用于计算 `in_sum_expr` 的按位或。只有 `BIT_OR(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | BIT_OR_SYM  '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_or(@$, $3, $5);
          }
```

##### `JSON_ARRAYAGG` 函数

标准语法：`JSON_ARRAYAGG(col_or_expr) [over_clause]`

用于将 `in_sum_expr` 中的值聚合为 Json 数组，详见 [MySQL 8.0 官方手册：14.19.1 Aggregate Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/aggregate-functions.html#function_json-arrayagg)。只有 `JSON_ARRAYAGG(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | JSON_ARRAYAGG '(' in_sum_expr ')' opt_windowing_clause
          {
            auto wrapper = make_unique_destroy_only<Json_wrapper>(YYMEM_ROOT);
            if (wrapper == nullptr) YYABORT;
            unique_ptr_destroy_only<Json_array> array{::new (YYMEM_ROOT)
                                                          Json_array};
            if (array == nullptr) YYABORT;
            $$ = NEW_PTN Item_sum_json_array(@$, $3, $5, std::move(wrapper),
                                             std::move(array));
          }
```

##### `JSON_OBJECTAGG` 函数

标准语法：`JSON_OBJECTAGG(key, value) [over_clause]`

用于以第 1 个 `in_sum_expr` 中的值为键，以第 2 个 `in_sum_expr` 中的值为值构造 Json 对象，详见 [MySQL 8.0 官方手册：14.19.1 Aggregate Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/aggregate-functions.html#function_json-objectagg)。只有 `JSON_OBJECTAGG(in_sum_expr, in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | JSON_OBJECTAGG '(' in_sum_expr ',' in_sum_expr ')' opt_windowing_clause
          {
            auto wrapper = make_unique_destroy_only<Json_wrapper>(YYMEM_ROOT);
            if (wrapper == nullptr) YYABORT;
            unique_ptr_destroy_only<Json_object> object{::new (YYMEM_ROOT)
                                                            Json_object};
            if (object == nullptr) YYABORT;
            $$ = NEW_PTN Item_sum_json_object(
                @$, $3, $5, $7, std::move(wrapper), std::move(object));
          }
```

##### `ST_COLLECT` 函数

用于将 `in_sum_expr` 中的空间元素构造为数据集，详见 [MySQL 8.0 官方手册：14.16.12 Spatial Aggregate Functions](https://dev.mysql.com/doc/refman/8.0/en/spatial-aggregate-functions.html)。有 `ST_COLLECT(in_sum_expr)` 和 `ST_COLLECT(DISTINCT in_sum_expr)` 两种备选语法， 可以作为窗口函数。Bison 语法如下：

```C++
        | ST_COLLECT_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_collect(@$, $3, $5, false);
          }
        | ST_COLLECT_SYM '(' DISTINCT in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_collect(@$, $4, $6, true );
          }
```

##### `BIT_XOR` 函数

标准语法：`BIT_XOR(expr) [over_clause]`

用于计算 `in_sum_expr` 的按位异或。只有 `BIT_XOR(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | BIT_XOR_SYM  '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_xor(@$, $3, $5);
          }
```

##### `COUNT` 函数

标准语法：

- `COUNT(expr) [over_clause]`
- `COUNT(DISTINCT expr,[expr...])`

`COUNT` 函数有如下三种语法结构：

- `COUNT(ALL *)` 或 `COUNT(*)` 语法：计算所有记录数，可以作为窗口函数。Bison 语法如下：

```C++
        | COUNT_SYM '(' opt_all '*' ')' opt_windowing_clause
          {
            $$= NEW_PTN PTI_count_sym(@$, $6);
          }
```

- `COUNT(in_sum_expr)` 语法：计算 `in_sum_expr` 非空的记录数，可以作为窗口函数。Bison 语法如下：

```C++
        | COUNT_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_count(@$, $3, $5);
          }
```

- `COUNT(DISTINCT expr_list)` 语法：计算 `expr_list` 字段均不同的记录数，可以作为窗口函数。Bison 语法如下：

```C++
        | COUNT_SYM '(' DISTINCT expr_list ')' opt_windowing_clause
          {
            $$= new Item_sum_count(@$, $4, $6);
          }
```

##### `MIN` 函数

标准语法：`MIN([DISTINCT] expr) [over_clause]`

用于计算 `in_sum_expr` 的最小值。有 `MIN(in_sum_expr)` 和 `MIN(DISTINCT in_sum_expr)` 两种备选语法，可以作为窗口函数。根据 ANSI SQL 的标准，允许在 `min` 和 `max` 分组函数中使用 `DISTINCT` 关键字，但实际上不会改变函数的行为和结果，即 `MIN|MAX(DISTINCT ...)` 的处理方式与普通的 `MIN|MAX()` 没有区别。Bison 语法如下：

```C++
        | MIN_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_min(@$, $3, $5);
          }
        | MIN_SYM '(' DISTINCT in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_min(@$, $4, $6);
          }
```

##### `MAX` 函数

标准语法：`MAX([DISTINCT] expr) [over_clause]`

用于计算 `in_sum_expr` 的最大值。有 `MAX(in_sum_expr)` 和 `MAX(DISTINCT in_sum_expr)` 两种备选语法，可以作为窗口函数。根据 ANSI SQL 的标准，允许在 `min` 和 `max` 分组函数中使用 `DISTINCT` 关键字，但实际上不会改变函数的行为和结果，即 `MIN|MAX(DISTINCT ...)` 的处理方式与普通的 `MIN|MAX()` 没有区别。Bison 语法如下：

```C++
        | MAX_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_max(@$, $3, $5);
          }
        | MAX_SYM '(' DISTINCT in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_max(@$, $4, $6);
          }
```

##### `STD` 函数

标准语法：`STD(expr) [over_clause]`

用于计算 `in_sum_expr` 的总体标准差。只有 `STD(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | STD_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_std(@$, $3, 0, $5);
          }
```

##### `VARIANCE` 函数

标准语法：`VARIANCE(expr) [over_clause]`

用于计算 `in_sum_expr` 的总体平方差。只有 `VARIANCE(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | VARIANCE_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_variance(@$, $3, 0, $5);
          }
```

##### `STDDEV_SAMP` 函数

标准语法：`STDDEV_SAMP(expr) [over_clause]`

用于计算 `in_sum_expr` 的样本标准差。只有 `STDDEV_SAMP(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | STDDEV_SAMP_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_std(@$, $3, 1, $5);
          }
```

##### `VAR_SAMP` 函数

标准语法：`VAR_SAMP(expr) [over_clause]`

用于计算 `in_sum_expr` 的样本平方差。只有 `VAR_SAMP(in_sum_expr)` 一种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | VAR_SAMP_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_variance(@$, $3, 1, $5);
          }
```

##### `SUM` 函数

标准语法：`SUM([DISTINCT] expr) [over_clause]`

用于计算 `in_sum_expr` 中元素之和。有 `SUM(in_sum_expr)` 和 `SUM(DISTINCT in_sum_expr)` 两种备选语法，可以作为窗口函数。Bison 语法如下：

```C++
        | SUM_SYM '(' in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_sum(@$, $3, false, $5);
          }
        | SUM_SYM '(' DISTINCT in_sum_expr ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_sum_sum(@$, $4, true, $6);
          }
```

##### `GROUP_CONCAT` 函数

标准语法：

```
GROUP_CONCAT([DISTINCT] expr [,expr ...]
             [ORDER BY {unsigned_integer | col_name | expr}
                 [ASC | DESC] [,col_name ...]]
             [SEPARATOR str_val])
```

用于将 `expr_list` 中的值拼接为字符串。在 `expr_list` 之前允许使用 `opt_distinct` 规则添加可选的 `DISTINCT` 关键字用于去重，在 `expr_lsit` 之后允许使用 `opt_gorder_clause` 关键字添加可选的 `ORDER BY` 子句用于排序，在可选的 `ORDER BY` 子句后允许使用 `opt_gconcat_separator` 规则添加可选的分隔符子句。Bison 语法如下：

```C++
        | GROUP_CONCAT_SYM '(' opt_distinct
          expr_list opt_gorder_clause
          opt_gconcat_separator
          ')' opt_windowing_clause
          {
            $$= NEW_PTN Item_func_group_concat(@$, $3, $4, $5, $6, $8);
          }
        ;
```

#### 语义组：`in_sum_expr`

`in_sum_expr` 语义组用于解析聚集函数的参数。

- 官方文档：[MySQL 8.0 参考手册：14.19.1 Aggregate Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/aggregate-functions.html#function_json-arrayagg)
- 标准语法：`[ALL] expr`
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。
- 使用场景：聚集函数的参数
- Bison 语法如下：

```C++
in_sum_expr:
          opt_all expr
          {
            $$= NEW_PTN PTI_in_sum_expr(@1, $2);
          }
        ;
```

> `expr` 语义组用于解析一般表达式。

#### 语义组：`opt_all`

`opt_all` 语义组用于解析可选的 `ALL` 关键字。

- 返回值类型：没有返回值
- Bison 语法如下：

```C++
opt_all:
          %empty
        | ALL
        ;
```

#### 语义组：`opt_windowing_clause`

`opt_windowing_clause` 语义组用于解析可选的窗口子句，如果包含窗口子句，则该聚集函数正在作为窗口函数使用。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`[{OVER (window_spec) | OVER window_name}]`
- 返回值类型：`PT_window` 类（`window`）
- Bison 语法如下：

```C++
opt_windowing_clause:
          %empty
          {
            $$= nullptr;
          }
        | windowing_clause
          {
            $$= $1;
          }
        ;
```

> `windowing_cluase` 语义组用于解析 `OVER` 引导的窗口子句，详见 [MySQL 源码｜38 - 语法解析(V2)：窗口函数](https://zhuanlan.zhihu.com/p/714780506)。

#### 语义组：`opt_distinct`

`opt_distanct` 语义组用于解析可选的 `DISTINCT` 关键字。

- 官方文档：[MySQL 8.0 参考手册：14.19.1 Aggregate Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/aggregate-functions.html#function_json-arrayagg)
- 标准语法：`[DISTINCT]`
- 返回值类型：`int`（`num`），当解析到 `DISTINCT` 关键字时返回 1，否则返回 0
- 使用场景：聚集函数 `GROUP_CONCAT`
- Bison 语法如下：

```C++
opt_distinct:
          %empty      { $$ = 0; }
        | DISTINCT    { $$ = 1; }
        ;
```

#### 语义组：`opt_gconcat_separator`

`opt_gconcat_separator` 语义组解析 `SEPARATOR` 关键字引导的分隔符号子句。

- 官方文档：[MySQL 8.0 参考手册：14.19.1 Aggregate Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/aggregate-functions.html#function_json-arrayagg)
- 标准语法：`[SEPARATOR str_val]`
- 返回值类型：`String` 对象（`string`）
- Bison 语法如下：

```C++
opt_gconcat_separator:
          %empty
          {
            $$= NEW_PTN String(",", 1, &my_charset_latin1);
            if ($$ == nullptr)
              MYSQL_YYABORT;
          }
        | SEPARATOR_SYM text_string { $$ = $2; }
        ;
```

> `text_string` 语义组用于解析单引号 / 双引号字符串、十六进制数或二进制数。

#### 语义组：`opt_gorder_clause`

`opt_gorder_clause` 语义组用于解析 `GROUP_CONCAT` 函数中的 `ORDER BY` 子句。

- 官方文档：[MySQL 8.0 参考手册：14.19.1 Aggregate Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/aggregate-functions.html#function_json-arrayagg)
- 标准语法：`[ORDER BY {unsigned_integer | col_name | expr} [ASC | DESC] [,col_name ...]]`
- 返回值类型：`PT_order_list` 对象（`order_list`）
- Bison 语法如下：

```C++
opt_gorder_clause:
          %empty                    { $$= nullptr; }
        | ORDER_SYM BY gorder_list  { $$= $3; }
        ;
```

