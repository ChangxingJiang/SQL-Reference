目录文档：[MySQL 源码｜源码剖析文档目录](https://zhuanlan.zhihu.com/p/714761054)

源码位置（版本 = MySQL 8.0.37）：[sql/sql_yacc.yy](https://github.com/mysql/mysql-server/blob/trunk/sql/sql_yacc.yy)

前置文档：

- [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554)

---

MySQL 解析窗口函数时，根据该函数是否同时为聚集函数，分为 `sum_expr` 语义组（针对同时也是聚集函数的窗口函数）和 `wiondow_func_call` 语义组（针对不是聚集函数的窗口函数）。在本节中，我们首先来梳理 `window_func_call` 语义组，其涉及语义组和语义组之间的关系如下图所示（图中绿色节点为字符串字面值涉及节点、蓝色节点为其他语义组、灰色节点为其他终结符）：

![语法解析-007-窗口函数](C:\blog\graph\MySQL源码剖析\语法解析-007-窗口函数.png)

#### 语义组：`window_func_call`

`window_func_call` 语义组用于解析不是窗口函数的聚集函数。

- 官方文档：
  - [MySQL 参考手册 - 14.20.1 Window Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/window-function-descriptions.html)
  - [MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：

```
over_clause:
    {OVER (window_spec) | OVER window_name}

window_spec:
    [window_name] [partition_clause] [order_clause] [frame_clause]

partition_clause:
    PARTITION BY expr [, expr] ...

order_clause:
    ORDER BY expr [ASC|DESC] [, expr [ASC|DESC]] ...
```

- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。

语义组中的备选规则如下：

##### `ROW_NUMBER()` 函数

标准语法：`ROW_NUMBER() over_clause`

用于计算当前记录在当前窗口分区中的顺序号，此序号从 1 开始；当排序字段相同时，会导致非确定的顺序，但不会出现相同序号。只有 `ROW_NUMBER() over_clause` 这一种备选语法。Bison 语法如下：

```C++
          ROW_NUMBER_SYM '(' ')' windowing_clause
          {
            $$=  NEW_PTN Item_row_number(@$, $4);
          }
```

##### `RANK()` 函数

标准语法：`RANK() over_clause`

用于计算当前记录在当前窗口分区中的排名，此排名从 1 开始；当排序字段相同时，所有字段值相同的字段会使用相同的排名，后续记录会跳过这些记录中未出现的排名。例如两条记录并列第 1 名，那么下一条记录将是第 3 名。Bison 语法如下：

```C++
        | RANK_SYM '(' ')' windowing_clause
          {
            $$= NEW_PTN Item_rank(@$, false, $4);
          }
```

##### `DENSE_RANK` 函数

标准语法：`DENSE_RANK() over_clause`

用于计算当前记录在当前窗口分区中的排名，此排名从 1 开始；与 `RANK()` 函数不同的是，当出现相同排名时，后续记录不会跳过这些记录中未出现的排名。例如两条记录并列第 1 名，那么下一条记录将是第 2 名。Bison 语法如下：

```C++
        | DENSE_RANK_SYM '(' ')' windowing_clause
          {
            $$= NEW_PTN Item_rank(@$, true, $4);
          }
```

##### `CUME_DIST()` 函数

标准语法：`CUME_DIST() over_clause`

用于计算当前记录在当前窗口分区中的累积概率分布，即当前记录之前（含当前记录和与当前记录排序字段相同的记录）的记录数除以窗口分区中的总行数。Bison 语法如下：

```C++
        | CUME_DIST_SYM '(' ')' windowing_clause
          {
            $$=  NEW_PTN Item_cume_dist(@$, $4);
          }
```

##### `PERCENT_RANK()` 函数

标准语法：`PERCENT_RANK() over_clause`

用于计算当前记录在当前窗口分区中的排名百分比，即除最大值外，当前记录之前（不含当前记录）的记录数除以窗口分区中的总记录数，计算公式为 $(rank - 1) / (rows - 1)$。Bison 语法如下：

```C++
        | PERCENT_RANK_SYM '(' ')' windowing_clause
          {
            $$= NEW_PTN Item_percent_rank(@$, $4);
          }
```

##### `NTILE()` 函数

标准语法：`NTILE(N) over_clause`

将当前窗口分区分为 N 组（桶），并返回当前记录在当前窗口分区中分到的桶编号，返回值范围为 $[1, N]$。N 必须为正整数。Bison 语法如下：

```C++
        | NTILE_SYM '(' stable_integer ')' windowing_clause
          {
            $$=NEW_PTN Item_ntile(@$, $3, $5);
          }
```

##### `LEAD()` 函数

标准语法：`LEAD(expr [, N[, default]]) [null_treatment] over_clause`

获取在当前窗口分区中，当前记录之后的第 n 条记录；如果这条记录不存在，则返回 default。N 必须是非负整数。N 的默认值为 1，default 的默认值为 NULL。Bison 语法如下：

```C++
        | LEAD_SYM '(' expr opt_lead_lag_info ')' opt_null_treatment windowing_clause
          {
            PT_item_list *args= NEW_PTN PT_item_list(@expr, @opt_lead_lag_info);
            if (args == nullptr || args->push_back($3))
              MYSQL_YYABORT; // OOM
            if ($4.offset != nullptr && args->push_back($4.offset))
              MYSQL_YYABORT; // OOM
            if ($4.default_value != nullptr && args->push_back($4.default_value))
              MYSQL_YYABORT; // OOM
            $$= NEW_PTN Item_lead_lag(@$, true, args, $6, $7);
          }
```

##### `LAG()` 函数

标准语法：`LAG(expr [, N[, default]]) [null_treatment] over_clause`

获取在当前窗口分区中，当前记录之前的第 n 条记录；如果这条记录不存在，则返回 default。N 必须是非负整数。N 的默认值为 1，default 的默认值为 NULL。Bison 语法如下：

```C++
        | LAG_SYM '(' expr opt_lead_lag_info ')' opt_null_treatment windowing_clause
          {
            PT_item_list *args= NEW_PTN PT_item_list(@expr, @opt_lead_lag_info);
            if (args == nullptr || args->push_back($3))
              MYSQL_YYABORT; // OOM
            if ($4.offset != nullptr && args->push_back($4.offset))
              MYSQL_YYABORT; // OOM
            if ($4.default_value != nullptr && args->push_back($4.default_value))
              MYSQL_YYABORT; // OOM
            $$= NEW_PTN Item_lead_lag(@$, false, args, $6, $7);
          }
```

##### `FIRST_VALUE()` 函数

标准语法：`FIRST_VALUE(expr) [null_treatment] over_clause`

用于获取当前分区窗口中的第 1 条记录。Bison 语法如下：

```C++
        | FIRST_VALUE_SYM '(' expr ')' opt_null_treatment windowing_clause
          {
            $$= NEW_PTN Item_first_last_value(@$, true, $3, $5, $6);
          }
```

##### `LAST_VALUE()` 函数

标准语法：`LAST_VALUE(expr) [null_treatment] over_clause`

用于获取当前分区窗口中的最后 1 条记录。Bison 语法如下：

```C++
        | LAST_VALUE_SYM  '(' expr ')' opt_null_treatment windowing_clause
          {
            $$= NEW_PTN Item_first_last_value(@$, false, $3, $5, $6);
          }
```

##### `NTH_VALUE()` 函数

标准语法：`NTH_VALUE(expr, N) [from_first_last] [null_treatment] over_clause`

用于获取当前分区窗口中的第 N 条记录。Bison 语法如下：

```C++
        | NTH_VALUE_SYM '(' expr ',' simple_expr ')' opt_from_first_last opt_null_treatment windowing_clause
          {
            PT_item_list *args= NEW_PTN PT_item_list(@expr, @simple_expr);
            if (args == nullptr ||
                args->push_back($3) ||
                args->push_back($5))
              MYSQL_YYABORT;
            $$= NEW_PTN Item_nth_value(@$, args, $7 == NFL_FROM_LAST, $8, $9);
          }
        ;
```

#### 语义组：`opt_from_first_last`

`opt_from_first_last` 语义组用于解析窗口计算顺序，即解析可选的 `FROM FIRST` 或 `FROM LAST`。默认使用 `FROM FIRST` 从窗口中的第一行开始计算，如果出现 `FROM LAST` 子句，MySQL 会将其解析并抛出一个异常。

- 官方文档：[MySQL 参考手册 - 14.20.1 Window Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/window-function-descriptions.html)
- 标准语法：`[from_first_last]`
- 返回值类型：`enum_from_first_last` 枚举值（`from_first_last`），其中包含 `NFL_NONE`、`NFL_FROM_FIRST` 和 `NFL_FROM_LAST` 这 3 个枚举值
- 备选规则和 Bison 语法：

| 备选规则         | 返回值                  | 备选规则含义                                                |
| ---------------- | ----------------------- | ----------------------------------------------------------- |
| `%empty`         | 枚举值 `NFL_NONE`       | 没有 `FROM FIRST` 或 `FROM LAST`（默认值，即 `FROM FIRST`） |
| `FROM FIRST_SYM` | 枚举值 `NFL_FROM_FIRST` | 解析 `FROM FIRST`（从窗口中的第一行开始计算）               |
| `FROM LAST_SYM`  | 枚举值 `NFL_FROM_LAST`  | 解析 `FROM LAST`（抛出异常）                                |

```C++
opt_from_first_last:
          %empty
          {
            $$= NFL_NONE;
          }
        | FROM FIRST_SYM
          {
            $$= NFL_FROM_FIRST;
          }
        | FROM LAST_SYM
          {
            $$= NFL_FROM_LAST;
          }
        ;
```

#### 语义组：`opt_null_treatment`

`opt_null_treatment` 语义组用于解析窗口中 NULL 值的处理方法，即解析可选的 `RESPECT NULLS` 或 `IGNORE NULLS`。默认使用 `RESPECT NULLS` 保留 NULL 值，如果出现 `IGNORE NULLS`，MySQL 会将其解析并抛出一个异常。

- 官方文档：[MySQL 参考手册 - 14.20.1 Window Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/window-function-descriptions.html)
- 标准语法：`[null_treatment]`
- 返回值类型：`enum_null_treatment` 枚举值（`null_treatment`），其中包含 `NT_NONE`、`NT_RESPECT_NULLS` 和 `NT_IGNORE_NULLS` 这 3 个枚举值
- 备选规则和 Bison 语法：

| 备选规则                | 返回值                    | 备选规则含义                                                 |
| ----------------------- | ------------------------- | ------------------------------------------------------------ |
| `%empty`                | 枚举值 `NT_NONE`          | 没有 `RESPECT NULLS` 或 `IGNORE NULLS`（默认值，即 `RESPECT NULLS`） |
| `RESPECT_SYM NULLS_SYM` | 枚举值 `NT_RESPECT_NULLS` | 解析 `RESPECT NULLS`（保留 NULL 值）                         |
| `IGNORE_SYM NULLS_SYM`  | 枚举值 `NT_IGNORE_NULLS`  | 解析 `IGNORE NULLS`（抛出异常）                              |

```C++
opt_null_treatment:
          %empty
          {
            $$= NT_NONE;
          }
        | RESPECT_SYM NULLS_SYM
          {
            $$= NT_RESPECT_NULLS;
          }
        | IGNORE_SYM NULLS_SYM
          {
            $$= NT_IGNORE_NULLS;
          }
        ;
```

#### 语义组：`opt_lead_lag_info`

`opt_lead_lag_info` 语义组用于解析标准语法 `[, N[, default]]`。

- 官方文档：[MySQL 参考手册 - 14.20.1 Window Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/window-function-descriptions.html)
- 标准语法：`[, N[, default]]`
- 使用场景：`LEAD()` 窗口函数和 `LAG()` 窗口函数
- 返回值类型：`lead_lag_info` 结构体

```C++
  struct {
    Item *offset;
    Item *default_value;
  } lead_lag_info;
```

- Bison 语法如下：

```C++
opt_lead_lag_info:
          %empty
          {
            $$.offset= nullptr;
            $$.default_value= nullptr;
          }
        | ',' stable_integer opt_ll_default
          {
            $$.offset= $2;
            $$.default_value= $3;
          }
        ;
```

> `stable_integer` 语义组用于解析 -9223372036854775808 到 18446744073709551615 之间的整数或预编译表达式的占位符，详见 [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554)。

#### 语义组：`opt_ll_default`

`opt_lead_lag_info` 语义组用于解析标准语法 `[, default]`。

- 官方文档：[MySQL 参考手册 - 14.20.1 Window Function Descriptions](https://dev.mysql.com/doc/refman/8.0/en/window-function-descriptions.html)
- 标准语法：`[, default]`
- 使用场景：`opt_ll_default` 语义组
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。
- Bison 语法如下：

```C++
opt_ll_default:
          %empty
          {
            $$= nullptr;
          }
        | ',' expr
          {
            $$= $2;
          }
        ;
```

#### 语义组：`windowing_clause`

`windowing_clause` 语义组用于解析窗口函数中 `OVER (window_spec)` 窗口子句或 `OVER window_name` 窗口子句。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`{OVER (window_spec) | OVER window_name}`（`over_clause`）
- 返回值类型：`PT_window` 类（`window`）
- Bison 语法如下：

```C++
windowing_clause:
          OVER_SYM window_name_or_spec
          {
            $$= $2;
          }
        ;
```

#### 语义组：`window_name_or_spec`

`window_name_or_spec` 语义组用于解析窗口函数 `OVER` 子句中的窗口子句或窗口名称。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`window_spec` 或 `window_name`
- 返回值类型：`PT_window` 类（`window`）
- Bison 语法如下：

```C++
window_name_or_spec:
          window_name
          {
            $$= NEW_PTN PT_window(@$, $1);
          }
        | window_spec
          {
            $$= $1;
          }
        ;
```

#### 语义组：`window_name`

`window_name` 语义组用于解析在 `WINDOW` 子句中定义的窗口名称。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`window_name`
- 返回值类型：`Item_string` 类（`item_string`）
- Bison 语法如下：

```C++
window_name:
          ident
          {
            $$= NEW_PTN Item_string($1.str, $1.length, YYTHD->charset());
          }
        ;
```

> `ident` 语义组解析标识符或任意未保留关键字，详见 [MySQL 源码｜41 - 语法解析(V2)：标识符 IDENT](https://zhuanlan.zhihu.com/p/714782314)。

#### 语义组：`window_spec`

`window_spec` 语义组用于解析窗口函数子句的标准语法 `(window_spec)`。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`(window_spec)`
- 返回值类型：`PT_window` 类（`window`）
- Bison 语法如下：

```C++
window_spec:
          '(' window_spec_details ')'
          {
            $$= $2;
            if ($$ != nullptr) $$->m_pos = @$;
          }
        ;
```

#### 语义组：`window_spec_details`

`window_spec_details` 语义组用于解析窗口函数子句括号内的标准语法 `window_spec`。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`[window_name] [partition_clause] [order_clause] [frame_clause]`（`window_spec`）
- 返回值类型：`PT_window` 类（`window`）
- Bison 语法如下：

```C++
window_spec_details:
           opt_existing_window_name
           opt_partition_clause
           opt_window_order_by_clause
           opt_window_frame_clause
           {
             auto frame= $4;
             if (!frame) // build an equivalent frame spec
             {
               auto start_bound= NEW_PTN PT_border(POS(), WBT_UNBOUNDED_PRECEDING);
               auto end_bound= NEW_PTN PT_border(POS(), $3 ? WBT_CURRENT_ROW :
                 WBT_UNBOUNDED_FOLLOWING);
               auto bounds= NEW_PTN PT_borders(POS(), start_bound, end_bound);
               frame= NEW_PTN PT_frame(POS(), WFU_RANGE, bounds, nullptr);
               frame->m_originally_absent= true;
             }
             $$= NEW_PTN PT_window(POS(), $2, $3, frame, $1);
           }
         ;
```

#### 语义组：`opt_existing_window_name`

`opt_existing_window_name` 语义组用于解析可选的窗口名称。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`[window_name]`
- 返回值类型：`Item_string` 类（`item_string`）
- Bison 语法如下：

```C++
opt_existing_window_name:
          %empty
          {
            $$= nullptr;
          }
        | window_name
          {
            $$= $1;
          }
        ;
```

#### 语义组：`opt_partition_clause`

`opt_partition_clause` 语义组用于解析窗口函数中可选的 `PARTITION BY` 子句。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`[partition_clause]`

```
partition_clause:
    PARTITION BY expr [, expr] ...
```

- 返回值类型：`PT_order_list` 类（`order_list`）
- Bison 语法如下：

```C++
opt_partition_clause:
          %empty
          {
            $$= nullptr;
          }
        | PARTITION_SYM BY group_list
          {
            $$= $3;
            if ($$ != nullptr) $$->m_pos = @$;
          }
        ;
```

> `group_list` 语义组用于解析分组字段的列表，详见 [MySQL 源码｜40 - 语法解析：GROUP BY 子句](https://zhuanlan.zhihu.com/p/714781362)。

#### 语义组：`opt_window_order_by_clause`

`opt_window_order_by_clause` 语义组用于解析窗口函数中可选的 `ORDER BY` 子句。

- 官方文档：[MySQL 参考手册 - 14.20.2 Window Function Concepts and Syntax](https://dev.mysql.com/doc/refman/8.0/en/window-functions-usage.html)
- 标准语法：`[order_clause]`

```
order_clause:
    ORDER BY expr [ASC|DESC] [, expr [ASC|DESC]] ...
```

- 返回值类型：`PT_order_list` 类（`order_list`）
- Bison 语法如下：

```C++
opt_window_order_by_clause:
          %empty
          {
            $$= nullptr;
          }
        | ORDER_SYM BY order_list
          {
            $$= $3;
            if ($$ != nullptr) $$->m_pos = @$;
          }
        ;
```

> `order_list` 语义组用于解析排序字段的列表，详见 [MySQL 源码｜39 - 语法解析：ORDER BY 子句](https://zhuanlan.zhihu.com/p/714781112)。

#### 语义组：`opt_window_frame_clause`

`opt_window_frame_clause` 语义组用于解析窗口函数中可选的帧子句（frame clause）。帧是当前分区（current partition）的一个子集，而帧子句则指定了如何定义这个子集。

帧是相对于当前行来确定的，这使得帧可以根据当前行在其分区内的位置在该分区内移动。例如：通过定义一个从分区开始到当前行的所有行作为帧，可以为每一行计算累计总和；又如通过定义一个帧为在当前行前后各延伸N行，可以计算滚动平均值。

- 官方文档：[MySQL 参考手册 - 14.20.3 Window Function Frame Specification](https://dev.mysql.com/doc/refman/8.0/en/window-functions-frames.html)
- 标准语法：`[frame_clause]`

```
frame_clause:
    frame_units frame_extent

frame_units:
    {ROWS | RANGE}
    
frame_extent:
    {frame_start | frame_between}

frame_between:
    BETWEEN frame_start AND frame_end

frame_start, frame_end: {
    CURRENT ROW
  | UNBOUNDED PRECEDING
  | UNBOUNDED FOLLOWING
  | expr PRECEDING
  | expr FOLLOWING
}
```

- 返回值类型：`PT_frame` 类（`window_frame`）
- Bison 语法如下：

```C++
opt_window_frame_clause:
          %empty
          {
            $$= nullptr;
          }
        | window_frame_units
          window_frame_extent
          opt_window_frame_exclusion
          {
            $$= NEW_PTN PT_frame(@$, $1, $2, $3);
          }
        ;
```

#### 语义组：`window_frame_units`

`opt_window_frame_clause` 语义组用于解析窗口函数中的标准语法 `{ROWS | RANGE}`。如果提供一个 `GROUPS` 则会被解析，并抛出一个异常。

- 官方文档：[MySQL 参考手册 - 14.20.3 Window Function Frame Specification](https://dev.mysql.com/doc/refman/8.0/en/window-functions-frames.html)
- 标准语法：`{ROWS | RANGE}`（`frame_units`）
- 返回值类型：枚举值 `enum_window_frame_unit`（`frame_units`），其中包含 `WFU_ROWS`、`WFU_RANGE` 和 `WFU_GROUPS`
- 备选规则和 Bison 语法如下：

| 备选规则     | 返回值              | 备选规则含义                              |
| ------------ | ------------------- | ----------------------------------------- |
| `ROWS_SYM`   | 枚举值 `WFU_ROWS`   | 解析 `ROWS`（使用开始和结束行位置定义帧） |
| `RANGE_SYM`  | 枚举值 `WFU_RANGE`  | 解析 `RANGE`（使用范围定义帧）            |
| `GROUPS_SYM` | 枚举值 `WFU_GROUPS` | 解析 `GROUP`（抛出异常）                  |

```C++
window_frame_units:
          ROWS_SYM    { $$= WFU_ROWS; }
        | RANGE_SYM   { $$= WFU_RANGE; }
        | GROUPS_SYM  { $$= WFU_GROUPS; }
        ;
```

#### 语义组：`window_frame_extent`

`window_frame_extent` 语义组用于解析窗口函数中的标准语法 `{frame_start | frame_between}`。

- 官方文档：[MySQL 参考手册 - 14.20.3 Window Function Frame Specification](https://dev.mysql.com/doc/refman/8.0/en/window-functions-frames.html)
- 标准语法：`{frame_start | frame_between}`（`frame_extent`）
- 返回值类型：`PT_borders` 类（`frame_extent`）
- Bison 语法如下：

```C++
window_frame_extent:
          window_frame_start
          {
            auto end_bound= NEW_PTN PT_border(@$, WBT_CURRENT_ROW);
            $$= NEW_PTN PT_borders(@$, $1, end_bound);
          }
        | window_frame_between
          {
            $$= $1;
          }
        ;
```

#### 语义组：`window_frame_start`

`window_frame_start` 语义组用于解析窗口函数中的帧开始位置，对应标准语法 `{CURRENT ROW | UNBOUNDED PRECEDING | expr PRECEDING}`。

- 官方文档：[MySQL 参考手册 - 14.20.3 Window Function Frame Specification](https://dev.mysql.com/doc/refman/8.0/en/window-functions-frames.html)
- 标准语法：`{CURRENT ROW | UNBOUNDED PRECEDING | expr PRECEDING}`
- 返回值类型：`PT_border` 类（`bound`）
- 备选方案和 Bison 语法如下：

| 备选规则                                   | 备选规则含义                       |
| ------------------------------------------ | ---------------------------------- |
| `UNBOUNDED_SYM PRECEDING_SYM`              | 从当前分区的第一行开始             |
| `NUM_literal PRECEDING_SYM`                | 从当前行之前第 NUM_literal 行开始  |
| `param_marker PRECEDING_SYM`               | 从当前行之前第 param_marker 行开始 |
| `INTERVAL_SYM expr interval PRECEDING_SYM` | 从当前行往前 expr 的时间长度开始   |
| `CURRENT_SYM ROW_SYM`                      | 从当前行开始                       |

```C++
window_frame_start:
          UNBOUNDED_SYM PRECEDING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_UNBOUNDED_PRECEDING);
          }
        | NUM_literal PRECEDING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_VALUE_PRECEDING, $1);
          }
        | param_marker PRECEDING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_VALUE_PRECEDING, $1);
          }
        | INTERVAL_SYM expr interval PRECEDING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_VALUE_PRECEDING, $2, $3);
          }
        | CURRENT_SYM ROW_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_CURRENT_ROW);
          }
        ;
```

> `NUM_literal` 语义组解析解析包含各类整数和小数在内的所有十进制数值字面值；`param_marker` 语义组解析预编译语句中的占位符；`expr` 语义组解析一般表达式；`interval` 语义组解析表示时间间隔的所有关键字。

#### 语义组：`window_frame_between`

`window_frame_between` 语义组用于解析窗口函数中的帧开始、结束位置，对应标准语法 `BETWEEN frame_start AND frame_end`。

- 官方文档：[MySQL 参考手册 - 14.20.3 Window Function Frame Specification](https://dev.mysql.com/doc/refman/8.0/en/window-functions-frames.html)
- 标准语法：`BETWEEN frame_start AND frame_end`
- 返回值类型：`PT_borders` 类（`frame_extent`）
- Bison 语法如下：

```C++
window_frame_between:
          BETWEEN_SYM window_frame_bound AND_SYM window_frame_bound
          {
            $$= NEW_PTN PT_borders(@$, $2, $4);
          }
        ;
```

#### 语义组：`window_frame_bound`

`window_frame_bound` 语义组用于解析窗口函数中的帧开始和结束位置，对应标准语法 `{CURRENT ROW | UNBOUNDED PRECEDING | UNBOUNDED FOLLOWING | expr PRECEDING | expr FOLLOWING}`，即在 `window_frame_start` 语义组的基础上增加了 `UNBOUNDED FOLLOWING` 和 `expr FOLLOWING`。

- 官方文档：[MySQL 参考手册 - 14.20.3 Window Function Frame Specification](https://dev.mysql.com/doc/refman/8.0/en/window-functions-frames.html)
- 标准语法：`{CURRENT ROW | UNBOUNDED PRECEDING | expr PRECEDING}`
- 返回值类型：`PT_border` 类（`bound`）
- Bison 语法如下：

```C++
window_frame_bound:
          window_frame_start
          {
            $$= $1;
          }
        | UNBOUNDED_SYM FOLLOWING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_UNBOUNDED_FOLLOWING);
          }
        | NUM_literal FOLLOWING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_VALUE_FOLLOWING, $1);
          }
        | param_marker FOLLOWING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_VALUE_FOLLOWING, $1);
          }
        | INTERVAL_SYM expr interval FOLLOWING_SYM
          {
            $$= NEW_PTN PT_border(@$, WBT_VALUE_FOLLOWING, $2, $3);
          }
        ;
```

#### 语义组：`opt_window_frame_exclusion`

`opt_window_frame_exclusion` 语义组用于解析窗口函数中的 `EXCLUDE` 子句，MySQL 不支持 `EXCLUDE` 子句，会解析 `EXCLUDE` 子句并抛出异常。

- 官方文档：[MySQL 参考手册 - 14.20.5 Window Function Restrictions](https://dev.mysql.com/doc/refman/8.0/en/window-function-restrictions.html)
- 返回值类型：`PT_exclusion` 类（`frame_exclusion`）
- Bison 语法如下：

```C++
opt_window_frame_exclusion:
          %empty
          {
            $$= nullptr;
          }
        | EXCLUDE_SYM CURRENT_SYM ROW_SYM
          {
            $$= NEW_PTN PT_exclusion(@$, WFX_CURRENT_ROW);
          }
        | EXCLUDE_SYM GROUP_SYM
          {
            $$= NEW_PTN PT_exclusion(@$, WFX_GROUP);
          }
        | EXCLUDE_SYM TIES_SYM
          {
            $$= NEW_PTN PT_exclusion(@$, WFX_TIES);
          }
        | EXCLUDE_SYM NO_SYM OTHERS_SYM
          { $$= NEW_PTN PT_exclusion(@$, WFX_NO_OTHERS);
          }
        ;
```







