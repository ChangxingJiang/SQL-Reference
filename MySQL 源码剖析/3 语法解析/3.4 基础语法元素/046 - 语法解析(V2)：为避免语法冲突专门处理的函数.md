 目录文档：[MySQL 源码｜源码剖析文档目录](https://zhuanlan.zhihu.com/p/714761054)

源码位置（版本 = MySQL 8.0.37）：[sql/sql_yacc.yy](https://github.com/mysql/mysql-server/blob/trunk/sql/sql_yacc.yy)

前置文档：

- [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554)

---

在上一节中，我们梳理了 `function_call_generic` 语义组，这节我们继续梳理为避免语法冲突专门处理的函数语义组 `function_call_conflict`。该语义组用于处理使用非保留关键字作为函数名的、使用常规语法的函数。因为这些非保留关键字还可能使用在其他语法结构中，所以需要特定的规则进行处理。其中涉及语义组和语义组之间的关系如下图所示（图中绿色节点为字符串字面值涉及节点、蓝色节点为其他语义组、灰色节点为其他终结符）：

![语法解析-012-为避免语法冲突专门处理的函数](C:\blog\graph\MySQL源码剖析\语法解析-012-为避免语法冲突专门处理的函数.png)

#### 语义组：`function_call_conflict`

`function_call_conflict` 语义组用于解析避免语法冲突专门处理的函数。

- 官方文档：
  - [MySQL 参考手册 - 14.4.2 Comparison Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/comparison-operators.html)
  - [MySQL 参考手册 - 14.5 Flow Control Functions](https://dev.mysql.com/doc/refman/8.0/en/flow-control-functions.html)
  - [MySQL 参考手册 - 14.6.2 Mathematical Functions](https://dev.mysql.com/doc/refman/8.0/en/mathematical-functions.html)
  - [MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)
  - [MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)
  - [MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。

语义组中的备选规则如下：

##### `ASCII` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`ASCII(str)`

`ASCII` 函数用于获取字符串最左侧元素的 ASCII 码值，Bison 语法如下：

```C++
          ASCII_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_ascii(@$, $3);
          }
```

##### `CHARSET` 函数

官方文档：[MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)

标准语法：`CHARSET(str)`

`CHARSET` 函数用于获取字符串的字符集，Bison 语法如下：

```C++
        | CHARSET '(' expr ')'
          {
            $$= NEW_PTN Item_func_charset(@$, $3);
          }
```

##### `COALESCE` 函数

官方文档：[MySQL 参考手册 - 14.4.2 Comparison Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/comparison-operators.html)

标准语法：`COALESCE(value,...)`

`COALESCE` 函数用于获取当前列表中第一个非空元素，Bison 语法如下：

```C++
        | COALESCE '(' expr_list ')'
          {
            $$= NEW_PTN Item_func_coalesce(@$, $3);
          }
```

##### `COLLATION` 函数

官方文档：[MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)

标准语法：`COLLATION(str)`

`COLLATION` 函数用于获取字符串的排序方式，Bison 语法如下：

```C++
        | COLLATION_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_collation(@$, $3);
          }
```

##### `DATABASE` 函数

官方文档：[MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)

标准语法：`DATABASE()`

`DATABASE` 函数用于获取当前数据库名称，Bison 语法如下：

```C++
        | DATABASE '(' ')'
          {
            $$= NEW_PTN Item_func_database(@$);
          }
```

##### `IF` 函数

官方文档：[MySQL 参考手册 - 14.5 Flow Control Functions](https://dev.mysql.com/doc/refman/8.0/en/flow-control-functions.html)

标准语法：`IF(expr1,expr2,expr3)`

`IF` 函数用于实现分支结构，Bison 语法如下：

```C++
        | IF '(' expr ',' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_if(@$, $3,$5,$7);
          }
```

##### `FORMAT` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`FORMAT(X,D[,locale])`

`FORMAT` 函数用于返回数字的指定格式，该规则有如下两种备选方案，分别对应是否包含 `locale` 参数的两种标准语法，Bison 语法如下：

```C++
        | FORMAT_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_format(@$, $3, $5);
          }
        | FORMAT_SYM '(' expr ',' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_format(@$, $3, $5, $7);
          }
```

##### `MICROSECOND` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`MICROSECOND(expr)`

`MICROSECOND` 函数用于获取时间的毫秒数，Bison 语法如下：

```C++
        | MICROSECOND_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_microsecond(@$, $3);
          }
```

##### `MOD` 函数

官方文档：[MySQL 参考手册 - 14.6.2 Mathematical Functions](https://dev.mysql.com/doc/refman/8.0/en/mathematical-functions.html)

标准语法：`MOD(N,M)`

`MOD` 函数用于进行取模运行，Bison 语法如下：

```C++
        | MOD_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_mod(@$, $3, $5);
          }
```

##### `QUARTER` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`QUARTER(date)`

`QUARTER` 函数用于计算日期所属的季度，Bison 语法如下：

```C++
        | QUARTER_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_quarter(@$, $3);
          }
```

##### `REPEAT` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`REPEAT(str,count)`

`REPEAT` 函数用于将一个函数复制多次，Bison 语法如下：

```C++
        | REPEAT_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_repeat(@$, $3,$5);
          }
```

##### `REPLACE` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`REPLACE(str,from_str,to_str)`

`REPLACE` 函数用于替换字符串中的子串，Bison 语法如下：

```C++
        | REPLACE_SYM '(' expr ',' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_replace(@$, $3,$5,$7);
          }
```

##### `REVERSE` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`REVERSE(str)`

`REVERSE` 函数用于将字符串倒序，Bison 语法如下：

```C++
        | REVERSE_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_reverse(@$, $3);
          }
```

##### `ROW_COUNT` 函数

官方文档：[MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)

标准语法：`ROW_COUNT()`

`ROW_COUNT` 函数用于获取上一个命令的影响行数，Bison 语法如下：

```C++
        | ROW_COUNT_SYM '(' ')'
          {
            $$= NEW_PTN Item_func_row_count(@$);
          }
```

##### `TRUNCATE` 函数

官方文档：[MySQL 参考手册 - 14.6.2 Mathematical Functions](https://dev.mysql.com/doc/refman/8.0/en/mathematical-functions.html)

标准语法：`TRUNCATE(X,D)`

`TRUNCATE` 函数用于阶段浮点数的小数位，Bison 语法如下：

```C++
        | TRUNCATE_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_round(@$, $3,$5,1);
          }
```

##### `WEEK` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`WEEK(date[,mode])`

`WEEK` 函数用于返回日期的星期数，Bison 语法如下：

```C++
        | WEEK_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_week(@$, $3, nullptr);
          }
        | WEEK_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_week(@$, $3, $5);
          }
```

##### `WEIGHT_STRING` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`WEIGHT_STRING(str [AS {CHAR|BINARY}(N)] [flags])`

`WEIGHT_STRING` 函数用于返回字符串的 weight string，该规则有四种备选方案，分别对应标准语法中的两个可选语法，Bison 语法如下：

```C++
        | WEIGHT_STRING_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_weight_string(@$, $3, 0, 0, 0);
          }
        | WEIGHT_STRING_SYM '(' expr AS CHAR_SYM ws_num_codepoints ')'
          {
            $$= NEW_PTN Item_func_weight_string(@$, $3, 0, $6, 0);
          }
        | WEIGHT_STRING_SYM '(' expr AS BINARY_SYM ws_num_codepoints ')'
          {
            $$= NEW_PTN Item_func_weight_string(@$, $3, 0, $6, 0, true);
          }
        | WEIGHT_STRING_SYM '(' expr ',' ulong_num ',' ulong_num ',' ulong_num ')'
          {
            $$= NEW_PTN Item_func_weight_string(@$, $3, $5, $7, $9);
          }
```

> `ulong_num` 语义组用于解析解析十进制整数或小数和十六进制数（转换为十进制数），返回 unsigned long 类型，详见 [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554)。

##### 地理相关函数

返回 `geometry_function` 语义组的解析结果。Bison 语法如下：

```C++
        | geometry_function
```

#### 语义组：`geometry_function`

`geometry_function` 规则用于解析各类地理位置相关函数。

- 官方文档：[MySQL 参考手册 - 14.16.5 MySQL-Specific Functions That Create Geometry Values](https://dev.mysql.com/doc/refman/8.0/en/gis-mysql-specific-functions.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。

语义组中的备选规则如下：

##### `GeomCollection` 函数

用于构造 `GeomCollection` 格式数据。

标准语法：`GeomCollection(g [, g] ...)`。Bison 语法如下：

```C++
          GEOMETRYCOLLECTION_SYM '(' opt_expr_list ')'
          {
            $$= NEW_PTN Item_func_spatial_collection(@$, $3,
                        Geometry::wkb_geometrycollection,
                        Geometry::wkb_point);
          }
```

##### `LineString` 函数

用于使用 `Point` 构造 `LineString` 格式数据。

标准语法：`LineString(pt [, pt] ...)`。Bison 语法如下：

```C++
        | LINESTRING_SYM '(' expr_list ')'
          {
            $$= NEW_PTN Item_func_spatial_collection(@$, $3,
                        Geometry::wkb_linestring,
                        Geometry::wkb_point);
          }
```

##### `MultiLineString` 函数

用于使用 `LineString` 构造 `MultiLineString` 格式数据。

标准语法：`MultiLineString(ls [, ls] ...)`。Bison 语法如下：

```C++
        | MULTILINESTRING_SYM '(' expr_list ')'
          {
            $$= NEW_PTN Item_func_spatial_collection(@$, $3,
                        Geometry::wkb_multilinestring,
                        Geometry::wkb_linestring);
          }
```

##### `MultiPoint` 函数

用于使用 `Point` 构造 `MultiPoint` 格式数据。

标准语法：`MultiPoint(pt [, pt2] ...)`。Bison 语法如下：

```C++
        | MULTIPOINT_SYM '(' expr_list ')'
          {
            $$= NEW_PTN Item_func_spatial_collection(@$, $3,
                        Geometry::wkb_multipoint,
                        Geometry::wkb_point);
          }
```

##### `MultiPolygon` 函数

用于使用 `Polygon` 构造 `MultiPolygon` 格式数据。

标准语法：`MultiPolygon(poly [, poly] ...)`。Bison 语法如下：

```C++
        | MULTIPOLYGON_SYM '(' expr_list ')'
          {
            $$= NEW_PTN Item_func_spatial_collection(@$, $3,
                        Geometry::wkb_multipolygon,
                        Geometry::wkb_polygon);
          }
```

##### `Point` 函数

用于使用坐标构造 `Point` 格式数据。

标准语法：`Point(x, y)`。Bison 语法如下：

```C++
        | POINT_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_point(@$, $3,$5);
          }
```

##### `Polygon` 函数

用于使用 `LineString` 的列表构造 `Polygon` 格式数据。

标准语法：`Polygon(ls [, ls] ...)`

```C++
        | POLYGON_SYM '(' expr_list ')'
          {
            $$= NEW_PTN Item_func_spatial_collection(@$, $3,
                        Geometry::wkb_polygon,
                        Geometry::wkb_linestring);
          }
        ;
```

#### 语义组：`ws_num_codepoints`

`ws_num_codepoints` 规则用于解析不为零的十进制整数和十六进制数（转换为十进制数）。若解析到 0 值则抛出异常。

- 返回值类型：`unsigned long` 类型（`ulong_num`）
- Bison 语法如下：

```C++
ws_num_codepoints:
        '(' real_ulong_num
        {
          if ($2 == 0)
          {
            YYTHD->syntax_error();
            MYSQL_YYABORT;
          }
        }
        ')'
        { $$= $2; }
        ;
```

> `real_ulong_num` 语义组用于解析十进制整数和十六进制数（转换为十进制数），返回 unsigned long 类型；如果输入十进制小数或超出范围的十进制整数，则抛出异常，详见 [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554)。
