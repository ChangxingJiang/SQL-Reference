目录文档：[MySQL 源码｜源码剖析文档目录](https://zhuanlan.zhihu.com/p/714761054)

源码位置（版本 = MySQL 8.0.37）：[sql/sql_yacc.yy](https://github.com/mysql/mysql-server/blob/trunk/sql/sql_yacc.yy)

前置文档：

- [MySQL 源码｜21 - 词法解析：状态转移逻辑梳理](https://zhuanlan.zhihu.com/p/714760407)
- [MySQL 源码｜33 - 语法解析：bison 基础语法规则](https://zhuanlan.zhihu.com/p/714779214)
- [MySQL 源码｜34 - 语法解析：所有 token 的名称与含义列表](https://zhuanlan.zhihu.com/p/714779441)

---

MySQL 解析函数时，根据函数名是否为关键字、是否可能引起冲突等，拆分为 `function_call_keyword`、`function_call_nonkeyword`、`function_call_generic` 和 `function_call_conflict` 这 4 个语义组。在本节中，我们首先来梳理 `function_call_keyword` 语义组，其涉及语义组和语义组之间的关系如下图所示（图中绿色节点为字符串字面值涉及节点、蓝色节点为其他语义组、灰色节点为其他终结符）：

![语法解析-009-关键字函数](C:\blog\graph\MySQL源码剖析\语法解析-009-关键字函数.png)

#### 语义组：`function_call_keyword`

`function_call_keyword` 语义组用于解析使用 SQL 2003 规范中关键字作为函数名的函数。因为函数名是关键字，因此不用考虑冲突的风险。

- 官方文档：
  - [MySQL 参考手册 - 14.4.2 Comparison Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/comparison-operators.html)
  - [MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)
  - [MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)
  - [MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)
  - [MySQL 参考手册 - 14.17.3 Functions That Search JSON Values](https://dev.mysql.com/doc/refman/8.0/en/json-search-functions.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。

语义组中的备选规则如下：

##### `CHAR` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`CHAR(N,... [USING charset_name])`

与 `CHAR` 函数相关的有如下两种备选方案，分别对应标准语法中是否包含 `USING charset_name` 的两种情形。Bison 语法如下：

```C++
          CHAR_SYM '(' expr_list ')'
          {
            $$= NEW_PTN Item_func_char(@$, $3);
          }
        | CHAR_SYM '(' expr_list USING charset_name ')'
          {
            $$= NEW_PTN Item_func_char(@$, $3, $5);
          }
```

##### `CURRENT_USER` 函数

官方文档：[MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)

标准语法：`CURRENT_USER` 和 `CURRENT_USER()`

与 `CURRENT_USER` 函数相关的只有依次匹配 `CURRENT_USER` 关键字、`optional_braces` 规则匹配结果（可选的空括号）这一种备选方案。Bison 语法如下：

```C++
        | CURRENT_USER optional_braces
          {
            $$= NEW_PTN Item_func_current_user(@$);
          }
```

##### `DATE` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`DATE(expr)`

与 `DATE` 函数相关的只有依次匹配 `DATE` 关键字、`(`、`expr` 规则匹配结果和 `)` 这一种备选方案。Bison 语法如下：

```C++
        | DATE_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_typecast_date(@$, $3);
          }
```

##### `DAY` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`DAY(expr)`

与 `DAY` 函数相关的只有依次匹配 `DAY` 关键字、`(`、`expr` 规则匹配结果和 `)` 这一种备选方案。Bison 语法如下：

```C++
        | DAY_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_dayofmonth(@$, $3);
          }
```

##### `HOUR` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`HOUR(expr)`

与 `HOUR` 函数相关的只有依次匹配 `HOUR` 关键字、`(`、`expr` 规则匹配结果和 `)` 这一种备选方案。Bison 语法如下：

```C++
        | HOUR_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_hour(@$, $3);
          }
```

##### `INSERT` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`INSERT(str,pos,len,newstr)`

与 `INSERT` 函数相关的只有依次匹配 `INSERT` 关键字、`(`、`expr` 规则匹配结果、`,`、`expr` 规则匹配结果、`,`、`expr` 规则匹配结果、`,`、`expr` 规则匹配结果和 `)` 这一种备选方案。Bison 语法如下：

```C++
        | INSERT_SYM '(' expr ',' expr ',' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_insert(@$, $3, $5, $7, $9);
          }
```

##### `INTERVAL` 函数

官方文档：[MySQL 参考手册 - 14.4.2 Comparison Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/comparison-operators.html)

标准语法：`INTERVAL(N,N1,N2,N3,...)`

与 `INTERVAL` 函数相关的如下 2 种备选方案：

- 依次匹配 `INTERVAL` 关键字、`(`、`expr` 规则匹配结果、`,`、`expr` 规则匹配结果和 `)`
- 依次匹配 `INTERVAL` 关键字、`(`、`expr` 规则匹配结果、`,`、`expr` 规则匹配结果、`,`、`expr_list` 规则匹配结果和 `)`

Bison 语法如下：

```C++
        | INTERVAL_SYM '(' expr ',' expr ')' %prec INTERVAL_SYM
          {
            $$= NEW_PTN Item_func_interval(@$, YYMEM_ROOT, $3, $5);
          }
        | INTERVAL_SYM '(' expr ',' expr ',' expr_list ')' %prec INTERVAL_SYM
          {
            $$= NEW_PTN Item_func_interval(@$, YYMEM_ROOT, $3, $5, $7);
          }
```

##### `JSON_VALUE` 函数

官方文档：[MySQL 参考手册 - 14.17.3 Functions That Search JSON Values](https://dev.mysql.com/doc/refman/8.0/en/json-search-functions.html)

标准语法：`JSON_VALUE(json_doc, path [RETURNING type] [on_empty] [on_error])`

- `on_empty`：`{NULL | ERROR | DEFAULT value} ON EMPTY`
- `on_error`：`{NULL | ERROR | DEFAULT value} ON ERROR`

与 `JSON_VALUE` 函数相关的只有依次匹配 `JSON_VALUE` 关键字、`(`、`simple_expr` 规则匹配结果、`,`、`text_literal` 规则匹配结果、`opt_returning_type` 规则匹配结果、`opt_on_empty_or_error` 规则匹配结果和 `)` 这一种备选方案。Bison 语法如下：

```C++
        | JSON_VALUE_SYM '(' simple_expr ',' text_literal
          opt_returning_type opt_on_empty_or_error ')'
          {
            $$= create_func_json_value(YYTHD, @3, $3, $5, $6,
                                       $7.empty.type, $7.empty.default_string,
                                       $7.error.type, $7.error.default_string);
          }
```

##### `LEFT` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`LEFT(str,len)`

与 `LEFT` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | LEFT '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_left(@$, $3, $5);
          }
```

##### `MINUTE` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`MINUTE(expr)`

与 `MINUTE` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | MINUTE_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_minute(@$, $3);
          }
```

##### `MONTH` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`MONTH(expr)`

与 `MONTH` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | MONTH_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_month(@$, $3);
          }
```

##### `RIGHT` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`RIGHT(str,len)`

与 `RIGHT` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | RIGHT '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_right(@$, $3, $5);
          }
```

##### `SECOND` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`SECOND(expr)`

与 `SECOND` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | SECOND_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_second(@$, $3);
          }
```

##### `TIME` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`TIME(expr)`

与 `TIME` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | TIME_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_typecast_time(@$, $3);
          }
```

##### `TIMESTAMP` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`TIMESTAMP(expr)` 和 `TIMESTAMP(expr1,expr2)`

与 `TIMESTAMP` 函数相关的有如下两种备选方案，Bison 语法如下：

```C++
        | TIMESTAMP_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_typecast_datetime(@$, $3);
          }
        | TIMESTAMP_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_add_time(@$, $3, $5, 1, 0);
          }
```

##### `TRIM` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`TRIM([{BOTH | LEADING | TRAILING} [remstr] FROM] str)` 和 `TRIM([remstr FROM] str)`

与 `TRIM` 函数相关的只有 8 种备选方案，来匹配以上标准语法的各个情形，Bison 语法如下：

```C++
        | TRIM '(' expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $3,
                                       Item_func_trim::TRIM_BOTH_DEFAULT);
          }
        | TRIM '(' LEADING expr FROM expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $6, $4,
                                       Item_func_trim::TRIM_LEADING);
          }
        | TRIM '(' TRAILING expr FROM expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $6, $4,
                                       Item_func_trim::TRIM_TRAILING);
          }
        | TRIM '(' BOTH expr FROM expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $6, $4, Item_func_trim::TRIM_BOTH);
          }
        | TRIM '(' LEADING FROM expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $5, Item_func_trim::TRIM_LEADING);
          }
        | TRIM '(' TRAILING FROM expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $5, Item_func_trim::TRIM_TRAILING);
          }
        | TRIM '(' BOTH FROM expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $5, Item_func_trim::TRIM_BOTH);
          }
        | TRIM '(' expr FROM expr ')'
          {
            $$= NEW_PTN Item_func_trim(@$, $5, $3,
                                       Item_func_trim::TRIM_BOTH_DEFAULT);
          }
```

##### `USER` 函数

官方文档：[MySQL 参考手册 - 14.15 Information Functions](https://dev.mysql.com/doc/refman/8.0/en/information-functions.html)

标准语法：`USER()`

与 `USER` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | USER '(' ')'
          {
            $$= NEW_PTN Item_func_user(@$);
          }
```

##### `YEAR` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`YEAR(expr)`

与 `YEAR` 函数相关的只有一种备选方案，Bison 语法如下：

```C++
        | YEAR_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_year(@$, $3);
          }
```

#### 语义组：`charset_name`

`charset_name` 语义组用于解析字符集名称。

- 返回值类型：`CHARSET_INFO` 结构体（`lexer.charset`）
- 使用场景：`CHAR()` 函数
- Bison 语法如下：

```C++
charset_name:
          ident_or_text
          {
            if (!($$=get_charset_by_csname($1.str,MY_CS_PRIMARY,MYF(0))))
            {
              my_error(ER_UNKNOWN_CHARACTER_SET, MYF(0), $1.str);
              MYSQL_YYABORT;
            }
            YYLIP->warn_on_deprecated_charset($$, $1.str);
          }
        | BINARY_SYM { $$= &my_charset_bin; }
        ;
```

> `ident_or_text` 语义组解析标识符、任意未保留关键字、单引号 / 双引号字符串或用户自定义变量；终结符 `BINARY_SYM` 解析关键字 `BINARY`。

#### 语义组：`optional_braces`

`optional_braces` 语义组用于解析可选的空括号。

- 返回值类型：没有返回值
- Bison 语法如下：

```C++
optional_braces:
          %empty {}
        | '(' ')' {}
        ;
```

#### 语义组：`opt_returning_type`

`opt_returning_type` 语义组用于解析可选的 `RETURNING` 引导的指定返回值类型语句。

- 返回值类型：`Cast_type` 结构体（`cast_type`），包含目标类型、字符集、长度和描述
- Bison 语法如下：

```C++
opt_returning_type:
          // The default returning type is CHAR(512). (The max length of 512
          // is chosen so that the returned values are not handled as BLOBs
          // internally. See CONVERT_IF_BIGGER_TO_BLOB.)
          %empty
          {
            $$= {ITEM_CAST_CHAR, nullptr, "512", nullptr};
          }
        | RETURNING_SYM cast_type
          {
            $$= $2;
          }
        ;
```

> `cast_type` 语义组用于解析 `CAST` 函数中使用的字段类型。

#### 语义组：`opt_on_empty_or_error`

`opt_on_empty_or_error` 语义组用于解析标准语法 `[on_empty] [on_error]`。

- 官方文档：[MySQL 参考手册 - 14.17.3 Functions That Search JSON Values](https://dev.mysql.com/doc/refman/8.0/en/json-search-functions.html)
- 标准语法：`[on_empty] [on_error]`

```
on_empty:
    {NULL | ERROR | DEFAULT value} ON EMPTY

on_error:
    {NULL | ERROR | DEFAULT value} ON ERROR
```

- 返回值类型：`json_on_error_or_empty` 结构体

```C++
struct {
    Json_on_response error;
    Json_on_response empty;
  } json_on_error_or_empty;
```

- 使用场景：`JSON_VALUE()` 函数

- Bison 语法如下：

```C++
opt_on_empty_or_error:
          %empty
          {
            $$.empty = {Json_on_response_type::IMPLICIT, nullptr};
            $$.error = {Json_on_response_type::IMPLICIT, nullptr};
          }
        | on_empty
          {
            $$.empty = $1;
            $$.error = {Json_on_response_type::IMPLICIT, nullptr};
          }
        | on_error
          {
            $$.error = $1;
            $$.empty = {Json_on_response_type::IMPLICIT, nullptr};
          }
        | on_empty on_error
          {
            $$.empty = $1;
            $$.error = $2;
          }
        ;
```

#### 语义组：`on_empty`

`on_empty` 语义组用于解析标准语法 `{NULL | ERROR | DEFAULT value} ON EMPTY`。

- 官方文档：[MySQL 参考手册 - 14.17.3 Functions That Search JSON Values](https://dev.mysql.com/doc/refman/8.0/en/json-search-functions.html)
- 标准语法：`{NULL | ERROR | DEFAULT value} ON EMPTY`
- 返回值类型：`json_on_response` 结构体

```C++
struct Json_on_response {
    Json_on_response_type type;
    Item *default_string;
  } json_on_response;
```

- Bison 语法如下：

```C++
on_empty:
          json_on_response ON_SYM EMPTY_SYM     { $$= $1; }
        ;
```

#### 语义组：`on_error`

`on_error` 语义组用于解析标准语法 `{NULL | ERROR | DEFAULT value} ON ERROR`。

- 官方文档：[MySQL 参考手册 - 14.17.3 Functions That Search JSON Values](https://dev.mysql.com/doc/refman/8.0/en/json-search-functions.html)
- 标准语法：`{NULL | ERROR | DEFAULT value} ON ERROR`
- 返回值类型：`json_on_response` 结构体
- Bison 语法如下：

```C++
on_error:
          json_on_response ON_SYM ERROR_SYM     { $$= $1; }
        ;
```

#### 语义组：`json_on_response`

`json_on_response` 语义组用于解析标准语法 `{NULL | ERROR | DEFAULT value}`。

- 官方文档：[MySQL 参考手册 - 14.17.3 Functions That Search JSON Values](https://dev.mysql.com/doc/refman/8.0/en/json-search-functions.html)
- 标准语法：`{NULL | ERROR | DEFAULT value}`
- 返回值类型：`json_on_response` 结构体
- Bison 语法如下：

```C++
json_on_response:
          ERROR_SYM
          {
            $$ = {Json_on_response_type::ERROR, nullptr};
          }
        | NULL_SYM
          {
            $$ = {Json_on_response_type::NULL_VALUE, nullptr};
          }
        | DEFAULT_SYM signed_literal
          {
            $$ = {Json_on_response_type::DEFAULT, $2};
          }
        ;
```

