目录文档：[MySQL 源码｜源码剖析文档目录](https://zhuanlan.zhihu.com/p/714761054)

源码位置（版本 = MySQL 8.0.37）：[sql/sql_yacc.yy](https://github.com/mysql/mysql-server/blob/trunk/sql/sql_yacc.yy)

前置文档：

- [MySQL 源码｜43 - 语法解析(V2)：关键字函数](https://zhuanlan.zhihu.com/p/714784157)
- [MySQL 源码｜59 - 语法解析(V2)：时间间隔字面值 & 时间字面值](https://zhuanlan.zhihu.com/p/716312316)

---

在上一节中，我们梳理了 `function_call_keyword` 语义组，这节我们继续梳理非关键字函数语义组 `function_call_nonkeyword`，其涉及语义组和语义组之间的关系如下图所示（图中绿色节点为字符串字面值涉及节点、蓝色节点为其他语义组、灰色节点为其他终结符）：

![语法解析-010-非关键字函数](C:\blog\graph\MySQL源码剖析\语法解析-010-非关键字函数.png)

#### 语义组：`function_call_nonkeyword`

`function_call_nonkeyword` 语义组用于解析非关键字函数。这些非保留关键字函数，有着特殊的语法结构，因此他们需要专用的语法结构，但仍然会存在潜在的语法冲突风险。

- 官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)；[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。

语义组中的备选规则如下：

##### `ADDDATE` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`ADDDATE(date,INTERVAL expr unit), ADDDATE(date,days)`

`ADDDATE` 函数用于在 `date` 的基础上增加 `unit` 个 `expr`，与 `ADDDATE` 函数相关的有如下两种备选方案，分别对应以上两种标准语法。Bison 语法如下：

```C++
          ADDDATE_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_date_add_interval(@$, $3, $5, INTERVAL_DAY, 0);
          }
        | ADDDATE_SYM '(' expr ',' INTERVAL_SYM expr interval ')'
          {
            $$= NEW_PTN Item_date_add_interval(@$, $3, $6, $7, 0);
          }
```

##### `CURDATE` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`CURDATE` 或 `CURDATE()`

`CURDATE` 函数用于获取当前日期，与 `CURDATE` 函数相关的只有如下一种备选方案，其中 `optional_braces` 规则对应可选的参数括号。Bison 语法如下：

```C++
        | CURDATE optional_braces
          {
            $$= NEW_PTN Item_func_curdate_local(@$);
          }
```

##### `CURTIME` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`CURTIME([fsp])`

`CURTIME` 函数用于获取当前时间，与 `CURTIME` 函数相关的只有如下一种备选方案，其中 `func_datetime_precision` 规则对应可选的空括号和包含一个整数的括号。Bison 语法如下：

```C++
        | CURTIME func_datetime_precision
          {
            $$= NEW_PTN Item_func_curtime_local(@$, static_cast<uint8>($2));
          }
```

##### `DATE_ADD` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`DATE_ADD(date,INTERVAL expr unit)`

`DATE_ADD` 函数用于在 `date` 的基础上增加 `unit` 个 `expr`，与 `DATE_ADD` 函数相关的只有如下一种备选方案，Bison 语法如下：

```C++
        | DATE_ADD_INTERVAL '(' expr ',' INTERVAL_SYM expr interval ')'
          %prec INTERVAL_SYM
          {
            $$= NEW_PTN Item_date_add_interval(@$, $3, $6, $7, 0);
          }
```

##### `DATE_SUB` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`DATE_SUB(date,INTERVAL expr unit)`

`DATE_ADD` 函数用于在 `date` 的基础上减去 `unit` 个 `expr`，与 `DATE_SUB` 函数相关的只有如下一种备选方案，Bison 语法如下：

```C++
        | DATE_SUB_INTERVAL '(' expr ',' INTERVAL_SYM expr interval ')'
          %prec INTERVAL_SYM
          {
            $$= NEW_PTN Item_date_add_interval(@$, $3, $6, $7, 1);
          }
```

##### `EXTRACT` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`EXTRACT(unit FROM date)`

`EXTRACT` 函数用于获取 `date` 中的某个部分（例如年、月、日等），与 `EXTRACT` 函数相关的只有如下一种备选方案，Bison 语法如下：

```C++
        | EXTRACT_SYM '(' interval FROM expr ')'
          {
            $$= NEW_PTN Item_extract(@$,  $3, $5);
          }
```

##### `GET_FORMAT` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`GET_FORMAT({DATE|TIME|DATETIME}, {'EUR'|'USA'|'JIS'|'ISO'|'INTERNAL'})`

`GET_FORMAT()` 函数用于获取 `date` 的指定格式字符串，与 `GET_FORMAT()` 函数相关的只有如下一种备选方案，Bison 语法如下：

```C++
        | GET_FORMAT '(' date_time_type  ',' expr ')'
          {
            $$= NEW_PTN Item_func_get_format(@$, $3, $5);
          }
```

##### `LOG` 函数

官方文档：[MySQL 参考手册 - 14.6.2 Mathematical Functions](https://dev.mysql.com/doc/refman/8.0/en/mathematical-functions.html)

标准语法：`LOG(X)` 或 `LOG(B,X)`

当 `LOG()` 函数只有一个参数时，`LOG()` 函数计算 `X` 的自然对数；当 `LOG()` 函数有两个参数时，`LOG()` 函数计算以 `B` 为底，`X` 的自然对数。与 `LOG()` 函数相关的有如下两种备选方案，分别对应有一个和两个参数的场景。Bison 语法如下：

```C++
        | LOG_SYM '(' expr ')'
          {
            $$= NEW_PTN Item_func_log(@$, $3);
          }
        | LOG_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_log(@$, $3, $5);
          }
```

##### `NOW` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`NOW([fsp])`

`NOW()` 函数用于获取当前的日期和时间，与 `NOW()` 函数相关的只有匹配 `rule` 规则匹配结果这一种备选方案，Bison 语法如下：

```C++
        | now
          {
            $$= NEW_PTN PTI_function_call_nonkeyword_now(@$,
              static_cast<uint8>($1));
          }
```

##### `POSITION` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法：`POSITION(substr IN str)`

`POSITION` 函数用于返回 `substr` 在 `str` 中的位置，与 `POSITION` 函数相关的有如下一种备选方案，Bison 语法如下：

```C++
        | POSITION_SYM '(' bit_expr IN_SYM expr ')'
          {
            $$= NEW_PTN Item_func_locate(@$, $5,$3);
          }
```

##### `SUB_DATE` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`SUBDATE(date,INTERVAL expr unit)` 或 `SUBDATE(expr,days)`

`SUBDATE` 函数用于在 `date` 的基础上减去 `unit` 个 `expr`，与 `SUBDATE` 函数相关的有如下两种备选方案，分别对应以上两种标准语法，Bison 语法如下：

```C++
        | SUBDATE_SYM '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_date_add_interval(@$, $3, $5, INTERVAL_DAY, 1);
          }
        | SUBDATE_SYM '(' expr ',' INTERVAL_SYM expr interval ')'
          {
            $$= NEW_PTN Item_date_add_interval(@$, $3, $6, $7, 1);
          }
```

##### `SUBSTRING` 函数

官方文档：[MySQL 参考手册 - 14.8 String Functions and Operators](https://dev.mysql.com/doc/refman/8.0/en/string-functions.html)

标准语法有如下 4 种：

- `SUBSTRING(str,pos)`
- `SUBSTRING(str FROM pos)`
- `SUBSTRING(str,pos,len)`
- `SUBSTRING(str FROM pos FOR len)`

`SUBSTRING` 函数用于在切分字符串，与 `SUBSTRING` 函数相关的有如下四种备选方案，分别对应以上四种标准语法，Bison 语法如下：

```C++
        | SUBSTRING '(' expr ',' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_substr(@$, $3,$5,$7);
          }
        | SUBSTRING '(' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_substr(@$, $3,$5);
          }
        | SUBSTRING '(' expr FROM expr FOR_SYM expr ')'
          {
            $$= NEW_PTN Item_func_substr(@$, $3,$5,$7);
          }
        | SUBSTRING '(' expr FROM expr ')'
          {
            $$= NEW_PTN Item_func_substr(@$, $3,$5);
          }
```

##### `SYSDATE` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`SYSDATE([fsp])`

`SYSDATE()` 函数用于获取当前系统日期和时间，与 `SYSDATE` 函数相关的只有如下一种备选方案，Bison 语法如下：

```C++
        | SYSDATE func_datetime_precision
          {
            $$= NEW_PTN PTI_function_call_nonkeyword_sysdate(@$,
              static_cast<uint8>($2));
          }
```

##### `TIMESTAMP_ADD` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`TIMESTAMPADD(unit,interval,datetime_expr)`

`TIMESTAMPADD(unit,interval,datetime_expr)` 函数用于向 `datetime_expr` 中增加 `unit` 单位个 `interval`。与 `TIMESTAMP_ADD` 函数相关的只有如下一种备选方案，Bison 语法如下：

```C++
        | TIMESTAMP_ADD '(' interval_time_stamp ',' expr ',' expr ')'
          {
            $$= NEW_PTN Item_date_add_interval(@$, $7, $5, $3, 0);
          }
```

##### `TIMESTAMP_DIFF` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`TIMESTAMPDIFF(unit,datetime_expr1,datetime_expr2)`

`TIMESTAMPDIFF(unit,datetime_expr1,datetime_expr2)` 用于计算 `timestamp_expr2 - timestamp_expr1` 的差，并返回以 `unit` 为单位的结果。与 `TIMESTAMPDIFF` 函数相关的只有如下一种备选方案，Bison 语法如下：

```C++
        | TIMESTAMP_DIFF '(' interval_time_stamp ',' expr ',' expr ')'
          {
            $$= NEW_PTN Item_func_timestamp_diff(@$, $5,$7,$3);
          }
```

##### `UTC_DATE` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`UTC_DATE` 或 `UTC_DATE()`

`UTC_DATE` 函数用于获取当前的 UTC 日期。与 `UTC_DATE` 函数相关的只有如下一种备选方案，其中 `optional_braces` 规则匹配一个可选的空括号，Bison 语法如下：

```C++
        | UTC_DATE_SYM optional_braces
          {
            $$= NEW_PTN Item_func_curdate_utc(@$);
          }
```

##### `UTC_TIME` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`UTC_TIME` 或 `UTC_TIME()`

`UTC_TIME` 函数用于获取当前 UTC 时间。与 `UTC_TIME` 函数相关的只有如下一种备选方案，其中 `func_datetime_precision` 规则匹配一个可选的空括号或包含 1 个参数的括号，Bison 语法如下：

```C++
        | UTC_TIME_SYM func_datetime_precision
          {
            $$= NEW_PTN Item_func_curtime_utc(@$, static_cast<uint8>($2));
          }
```

##### `UTC_TIMESTAMP` 函数

官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)

标准语法：`UTC_TIMESTAMP` 或 `UTC_TIMESTAMP([fsp])`

`UTC_TIMESTAMP` 函数用于获取当前 UTC 的日期和时间。与 `UTC_TIMESTAMP` 函数相关的只有如下一种备选方案，其中 `func_datetime_precision` 规则匹配一个可选空括号或包含 1 个参数的括号，Bison 语法如下：

```C++
        | UTC_TIMESTAMP_SYM func_datetime_precision
          {
            $$= NEW_PTN Item_func_now_utc(@$, static_cast<uint8>($2));
          }
```

在 `function_call_nonkeyword` 规则中，使用了 `expr`、`interval`、`optional_braces`、`func_datetime_precision`、`date_time_type`、`bit_expr`、`interval_time_stamp` 这些规则。其中：

- `expr`、`bit_expr` 规则待梳理表达式时梳理
- `optional_braces` 规则在 [MySQL 源码｜43 - 语法解析：关键字函数](https://zhuanlan.zhihu.com/p/714784157) 中已经梳理
- `interval`、`interval_time_stamp` 规则在 [MySQL 源码｜59 - 语法解析(V2)：时间间隔字面值 & 时间字面值](https://zhuanlan.zhihu.com/p/716312316) 中已经梳理
- `func_datetime_precision`、`date_time_type` 在下文中梳理

#### 语义组：`func_datetime_precision`

`func_datetime_precision` 语义组用于解析日期格式字符串、时间格式字符串或日期 + 时间格式字符串。

- 官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)
- 标准语法：`([fsp])`
- 返回值类型：`unsigned long`（`ulong_num`）
- 使用场景：`CURTIME()` 函数、`SYSDATE()` 函数、`UTC_TIME()` 函数和 `UTC_TIMESTAMP()` 函数等
- Bison 语法如下：

```C++
func_datetime_precision:
          %empty { $$= 0; }
        | '(' ')'                    { $$= 0; }
        | '(' NUM ')'
           {
             int error;
             $$= (ulong) my_strtoll10($2.str, nullptr, &error);
           }
        ;
```

#### 语义组：`date_time_type`

`date_time_type` 语义组用于解析 `DATE`、`TIME`、`TIMESTAMP` 或 `DATETIME` 关键字。其中 `TIMESTAMP` 关键字的返回值与 `DATETIME` 关键字的返回值一致。

- 官方文档：[MySQL 参考手册 - 14.7 Date and Time Functions](https://dev.mysql.com/doc/refman/8.0/en/date-and-time-functions.html)
- 标准语法：`{DATE|TIME|TIMESTAMP|DATETIME}`
- 返回值类型：`enum_mysql_timestamp_type` 枚举值（`date_time_type`），包含 `MYSQL_TIMESTAMP_NONE`、`MYSQL_TIMESTAMP_ERROR`、`MYSQL_TIMESTAMP_DATE`、`MYSQL_TIMESTAMP_DATETIME`、`MYSQL_TIMESTAMP_TIME` 和 `MYSQL_TIMESTAMP_DATETIME_TZ` 这 6 个枚举值
- Bison 语法如下：

```C++
date_time_type:
          DATE_SYM  {$$= MYSQL_TIMESTAMP_DATE; }
        | TIME_SYM  {$$= MYSQL_TIMESTAMP_TIME; }
        | TIMESTAMP_SYM {$$= MYSQL_TIMESTAMP_DATETIME; }
        | DATETIME_SYM  {$$= MYSQL_TIMESTAMP_DATETIME; }
        ;
```
