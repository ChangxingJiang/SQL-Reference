目录文档：[MySQL 源码｜源码剖析文档目录](https://zhuanlan.zhihu.com/p/714761054)

源码位置（版本 = MySQL 8.0.37）：[/sql/sql_yacc.yy](https://github.com/mysql/mysql-server/blob/trunk/sql/sql_yacc.yy)

前置文档：

- [MySQL 源码｜33 - 语法解析：bison 基础语法规则](https://zhuanlan.zhihu.com/p/714779214)
- [MySQL 源码｜59 - 语法解析(V2)：时间间隔字面值 & 时间字面值](https://zhuanlan.zhihu.com/p/716312316)
- [MySQL 源码｜65 - 语法解析(V2)：字符串字面值](https://zhuanlan.zhihu.com/p/717934287)
- [MySQL 源码｜64 - 语法解析(V2)：非保留关键字](https://zhuanlan.zhihu.com/p/717740054)
- [MySQL 源码｜41 - 语法解析(V2)：标识符 IDENT](https://zhuanlan.zhihu.com/p/714782314)
- [MySQL 源码｜66 - 语法解析(V2)：预编译表达式的参数值](https://zhuanlan.zhihu.com/p/718323872)
- [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554)

---

根据 [MySQL 参考手册 - 11.1 Literal Values](https://dev.mysql.com/doc/refman/8.4/en/literals.html)，MySQL 中有字符串字面值、数值型字面值、日期和时间型字面值、十六进制字面值、二进制字面值、布尔字面值和空值字面值这 7 类字面值。其中：

- 字符串字面值在 [MySQL 源码｜65 - 语法解析(V2)：字符串字面值](https://zhuanlan.zhihu.com/p/717934287) 中梳理
- 数值型字面值在 [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554) 中梳理
- 日期和时间字面值在 [MySQL 源码｜59 - 语法解析(V2)：时间间隔字面值 & 时间字面值](https://zhuanlan.zhihu.com/p/716312316) 中梳理
- 十六进制字面值通常用作字符串或数值，分别在在 [MySQL 源码｜65 - 语法解析(V2)：字符串字面值](https://zhuanlan.zhihu.com/p/717934287) 和 [MySQL 源码｜67 - 语法解析(V2)：数值字面值](https://zhuanlan.zhihu.com/p/718508554) 中梳理
- 二进制字面值除在通用字面值中使用外，仅在 `text_string` 语义组中使用，已在 [MySQL 源码｜65 - 语法解析(V2)：字符串字面值](https://zhuanlan.zhihu.com/p/717934287) 中梳理
- 布尔字面值除在通用字面值中使用外，仅在 `IS TRUE` 和 `IS FALSE` 中直接使用终结符
- 空值字面值除在通用字面值中使用外，主要在 `IS NULL`、`IS NOT NULL` 等场景中直接使用终结符

下面，我们来梳理通用字面值相关语义组，其中涉及的 symbol 及 symbol 之间的关系如下（图中绿色节点为字符串字面值涉及节点、蓝色节点为其他语义组、灰色节点为其他终结符）：

![语法解析-006-通用字面值](C:\blog\graph\MySQL源码剖析\语法解析-006-通用字面值.png)

#### 语义组：`literal`（通用非空值字面值）

`literal` 语义组用于解析字符串字面值（`text_literal`）、数值字面值（`NUM_literal`）、日期和时间类型字面值（`temporal_literal`）、布尔字面值（`FALSE_SYM` 和 `TRUE_SYM`）、十六进制字面值（`HEX_NUM`）、二进制字面值（`BIN_NUM`）、指定字符集的十六进制字面值（`UNDERSCORE_CHARSET HEX_NUM`）和指定字符集的二进制字面值（`UNDERSCORE_CHARSET BIN_NUM`）。其中终结符 `UNDERSCORE_CHARSET` 表示以 `_` 开头的字符集名称，语法逻辑参考 [MySQL 参考手册 - 12.2.1 Character Set Repertoire](https://dev.mysql.com/doc/refman/8.4/en/charset-repertoire.html)。

- 官方文档：[MySQL 参考手册 - 11.1 Literal Values](https://dev.mysql.com/doc/refman/8.4/en/literals.html)
- 使用场景：`literal_or_null` 语义组、`signed_literal` 语义组
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。
- 备选规则和 Bison 语法如下：

| 备选规则                     | 返回值类型                                                   | 备选规则含义                     |
| ---------------------------- | ------------------------------------------------------------ | -------------------------------- |
| `text_literal`               | `PTI_text_literal` 对象                                      | 字符串字面值                     |
| `NUM_literal`                | `Item_int` 对象、`Item_uint` 对象、`Item_decimal` 对象或 `Item_float` 对象 | 数值型字面值                     |
| `temporal_literal`           | `PTI_temporal_literal` 对象                                  | 日期或时间类型字面值             |
| `FALSE_SYM`                  | `Item_func_false` 对象                                       | 布尔字面值（假值）               |
| `TRUE_SYM`                   | `Item_func_true` 对象                                        | 布尔字面值（真值）               |
| `HEX_NUM`                    | `Item_hex_string` 对象                                       | 十六进制字符串字面值             |
| `BIN_NUM`                    | `Item_bin_string` 对象                                       | 二进制字符串字面值               |
| `UNDERSCORE_CHARSET HEX_NUM` | `PTI_literal_underscore_charset_hex_num` 对象                | 指定字符集的十六进制字符串字面值 |
| `UNDERSCORE_CHARSET BIN_NUM` | `PTI_literal_underscore_charset_bin_num` 对象                | 指定字符集的二进制字符串字面值   |

```C++
literal:
          text_literal { $$= $1; }
        | NUM_literal  { $$= $1; }
        | temporal_literal
        | FALSE_SYM
          {
            $$= NEW_PTN Item_func_false(@$);
          }
        | TRUE_SYM
          {
            $$= NEW_PTN Item_func_true(@$);
          }
        | HEX_NUM
          {
            $$= NEW_PTN Item_hex_string(@$, $1);
          }
        | BIN_NUM
          {
            $$= NEW_PTN Item_bin_string(@$, $1);
          }
        | UNDERSCORE_CHARSET HEX_NUM
          {
            $$= NEW_PTN PTI_literal_underscore_charset_hex_num(@$, $1, $2);
          }
        | UNDERSCORE_CHARSET BIN_NUM
          {
            $$= NEW_PTN PTI_literal_underscore_charset_bin_num(@$, $1, $2);
          }
        ;
```

#### 语义组：`null_as_literal`（空值字面值）

`null_as_literal` 语义组用于解析 `NULL` 关键字并将其视作空值字面值。

- 官方文档：[MySQL 参考手册 - 11.1.7 NULL Values](https://dev.mysql.com/doc/refman/8.4/en/null-values.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类；实际返回 `Item_null` 对象。
- Bison 语法如下：

```C++
null_as_literal:
          NULL_SYM
          {
            Lex_input_stream *lip= YYLIP;
            /*
              For the digest computation, in this context only,
              NULL is considered a literal, hence reduced to '?'
              REDUCE:
                TOK_GENERIC_VALUE := NULL_SYM
            */
            lip->reduce_digest_token(TOK_GENERIC_VALUE, NULL_SYM);
            $$= NEW_PTN Item_null(@$);
          }
        ;
```

#### 语义组：`signed_literal`

`signed_literal` 语义组用于解析通用字面值，或包含 `+` 或 `-` 前缀的数值型字面值。

- 官方文档：[MySQL 参考手册 - 11.1.2 Numeric Literals](https://dev.mysql.com/doc/refman/8.4/en/number-literals.html)
- 返回值：`Item` 类（`item`），用于表示查询任何类型表达式的基类。
- 备选规则和 Bison 语法如下：

| 备选规则          | 返回值类型 | 备选规则含义                |
| ----------------- | ---------- | --------------------------- |
| `literal`         | `Item` 类  | 各类通用字面值              |
| `'+' NUM_literal` | `Item` 类  | 包含 `+` 前缀的数值型字面值 |
| `'-' NUM_literal` | `Item` 类  | 包含 `-` 前缀的数值型字面值 |

```C++
signed_literal:
          literal
        | '+' NUM_literal { $$= $2; }
        | '-' NUM_literal
          {
            if ($2 == nullptr)
              MYSQL_YYABORT; // OOM
            $2->max_length++;
            $$= $2->neg();
          }
        ;
```

#### 语义组：`signed_literal_or_null`

`signed_literal_or_null` 语义组用于解析通用字面值、包含 `+` 或 `-` 前缀的数值型字面值和空值字面值。

- 官方文档：[MySQL 参考手册 - 11.1 Literal Values](https://dev.mysql.com/doc/refman/8.4/en/literals.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。
- Bison 语法如下：

```C++
signed_literal_or_null:
          signed_literal
        | null_as_literal
        ;
```

#### 语义组：`literal_or_null`

`literal_or_null` 语义组用于解析通用字面值或空值字面值。

- 官方文档：[MySQL 参考手册 - 11.1 Literal Values](https://dev.mysql.com/doc/refman/8.4/en/literals.html)
- 返回值类型：`Item` 类（`item`），用于表示查询任何类型表达式的基类。
- Bison 语法如下：

```C++
literal_or_null:
          literal
        | null_as_literal
        ;
```
